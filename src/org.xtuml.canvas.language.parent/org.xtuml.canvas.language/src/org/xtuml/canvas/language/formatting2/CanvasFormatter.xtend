/*
 * generated by Xtext 2.22.0
 */
package org.xtuml.canvas.language.formatting2

import com.google.inject.Inject
import org.xtuml.canvas.language.canvas.Model
import org.xtuml.canvas.language.services.CanvasGrammarAccess
import org.xtuml.canvas.language.canvas.Shape
import org.xtuml.canvas.language.canvas.GraphicalElement
import org.xtuml.canvas.language.canvas.Connectors
import org.xtuml.canvas.language.canvas.Connector
import org.eclipse.xtext.formatting2.IFormattableDocument
import org.eclipse.xtext.formatting2.AbstractFormatter2
import org.eclipse.emf.common.util.EList
import org.xtuml.canvas.language.canvas.Shapes
import org.xtuml.canvas.language.canvas.ModelProperties
import org.xtuml.canvas.language.canvas.Polyline
import org.xtuml.canvas.language.canvas.Segment
import org.xtuml.canvas.language.canvas.Anchors
import org.xtuml.canvas.language.canvas.FloatingTexts
import org.xtuml.canvas.language.canvas.StartAnchor
import org.xtuml.canvas.language.canvas.EndAnchor
import org.xtuml.canvas.language.canvas.FloatingText
import org.xtuml.canvas.language.canvas.Bounds
import org.xtuml.canvas.language.canvas.Layers
import org.xtuml.canvas.language.canvas.Layer
import org.xtuml.canvas.language.canvas.ModelPropertiesItem
import org.xtuml.canvas.language.canvas.Styles
import org.xtuml.canvas.language.canvas.StyleItem

class CanvasFormatter extends AbstractFormatter2 {

	@Inject extension CanvasGrammarAccess
	
	var spacing = 2
	var whitespace = " "

	def dispatch void format(Model it, extension IFormattableDocument document) {
		formatModel(document)
	}

	def void formatModel(Model it, extension IFormattableDocument document) {
		allRegionsFor.keyword("properties").prepend[newLines = 2]
		allRegionsFor.keyword("shapes").prepend[newLines = 2]
		allRegionsFor.keyword("connectors").prepend[newLines = 2]
		regionFor.keyword("render").prepend[newLines = 2]
		format(it.properties, document)
		it.elements.forEach[format]
		it.layers.format
	}
	var calculated = newHashMap()
	def String getSpacing(int level) {
		var result = calculated.get(level);
		if(result === null) {
			result = whitespace.repeat(spacing * level)
			calculated.put(level, result)
		}
		return result as String
	}

	def dispatch void format(ModelProperties it, extension IFormattableDocument document) {
	    it.propertyItems.forEach[format]
	}

	def dispatch void format(ModelPropertiesItem it, extension IFormattableDocument document) {
		regionFor.keyword("viewport").prepend[space = '\n' + getSpacing(1)]
		regionFor.keyword("zoom").prepend[space = '\n' + getSpacing(1)]
		regionFor.keyword("background_color").prepend[space = '\n' + getSpacing(1)]
	}
	
	def dispatch void format(Styles it, extension IFormattableDocument document) {
		regionFor.keyword("styles").prepend[space = '\n' + getSpacing(2)]
	    it.style_items.forEach[format]
	}

	def dispatch void format(StyleItem it, extension IFormattableDocument document) {
		regionFor.keyword("fill_color").prepend[space = '\n' + getSpacing(3)]
		regionFor.keyword("line_color").prepend[space = '\n' + getSpacing(3)]
	}
	
	def dispatch void format(GraphicalElement element, extension IFormattableDocument document) {
		if(element instanceof Shapes) {
			(element as Shapes).shapes.forEach[format]
		}
		if(element instanceof Connectors) {
			(element as Connectors).connectors.forEach[format]
		}
	}

	def dispatch void format(Shape it, extension IFormattableDocument document) {
		regionFor.keyword("shape").prepend[space = '\n' + getSpacing(1)]
		regionFor.keyword("render").prepend[space = '\n' + getSpacing(2)]
		regionFor.keyword("layers").prepend[space = '\n' + getSpacing(2)]
		format(it.bounds, 2, document)
		it.text.regionFor.keyword("text").prepend[space = '\n' + getSpacing(2)]
		it.styles.format
	}

	def dispatch void format(Connector it, extension IFormattableDocument document) {
		regionFor.keyword("connector").prepend[space = '\n' + getSpacing(1)]
		regionFor.keyword("render").prepend[space = '\n' + getSpacing(2)]
		regionFor.keyword("layers").prepend[space = '\n' + getSpacing(2)]
		it.polyline.format
		it.anchors.format
		it.texts.format
		it.styles.format
	}
	
	def dispatch void format(Polyline it, extension IFormattableDocument document) {
		regionFor.keyword("polyline").prepend[space = '\n' + getSpacing(2)]
		it.segments.forEach[format]
	}
	
	def dispatch void format(Segment it, extension IFormattableDocument document) {
		regionFor.keyword("segment").prepend[space = '\n' + getSpacing(3)]
		regionFor.keyword("start").prepend[space = '\n' + getSpacing(4)]
		regionFor.keyword("end").prepend[space = '\n' + getSpacing(4)]
		it.endPoint.prepend[space = '   ']
	}
	
	def dispatch void format(Anchors it, extension IFormattableDocument document) {
		regionFor.keyword("anchors").prepend[space = '\n' + getSpacing(2)]
		it.startAnchor.format
		it.endAnchor.format
	}
	
	def dispatch void format(StartAnchor it, extension IFormattableDocument document) {
		regionFor.keyword("start").prepend[space = '\n' + getSpacing(3)]
		it.point.regionFor.keyword("point").prepend[space = '\n' + getSpacing(4)]
		it.anchor.regionFor.keyword("shape").prepend[space = '\n' + getSpacing(4)]
		it.anchor.regionFor.keyword("connector").prepend[space = '\n' + getSpacing(4)]
	}
	
	def dispatch void format(EndAnchor it, extension IFormattableDocument document) {
		regionFor.keyword("end").prepend[space = '\n' + getSpacing(3)]
		it.point.regionFor.keyword("point").prepend[space = '\n' + getSpacing(4)]
		it.anchor.regionFor.keyword("shape").prepend[space = '\n' + getSpacing(4)]
		it.anchor.regionFor.keyword("connector").prepend[space = '\n' + getSpacing(4)]
	}
	
	def dispatch void format(FloatingTexts it, extension IFormattableDocument document) {
		regionFor.keyword("texts").prepend[space = '\n' + getSpacing(2)]
		it.texts.forEach[format]
	}
	
	def dispatch void format(FloatingText it, extension IFormattableDocument document) {
		regionFor.keyword("text").prepend[space = '\n' + getSpacing(3)]
		format(it.bounds, 4, document)
		it.regionFor.keyword("where").prepend[space = '\n' + getSpacing(4)]
	}
	
	def void format(Bounds it, int spacing, extension IFormattableDocument document) {
		regionFor.keyword("bounds").prepend[space = '\n' + getSpacing(spacing)]
//		regionFor.keywords("x", "y", "width", "height").forEach[prepend[space = '\n' + getSpacing(spacing + 1)]]
	}
	
	def dispatch void format(Layers it, extension IFormattableDocument document) {
	    regionFor.keyword("layers").prepend[newLines = 2]
	    it.layers.forEach[format]
	}
	
	def dispatch void format(Layer it, extension IFormattableDocument document) {
	    regionFor.keyword("layer").prepend[space = '\n' + getSpacing(1)]
	}
	
}
